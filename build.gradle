plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.2' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
}

allprojects {
    group = 'me.suhsaechan'
    version = '1.2.2'
    
    repositories {
        mavenCentral()

        // spring-integration-smb 문제로 인해 주석 처리
        // maven { url 'https://repo.spring.io/plugins-release/' }

        maven {
            name = 'SUH-NEXUS'
            url "https://nexus.suhsaechan.me/repository/maven-releases/"
            metadataSources {
                mavenPom()
                artifact()
            }
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }
    
    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }
    
    dependencies {
        // 모든 모듈 공통 기본 의존성
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
        
        // 테스트 관련 기본 의존성
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.springframework.security:spring-security-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }
    
    tasks.named('test') {
        useJUnitPlatform()
    }
}

// 루트 프로젝트 테스트 설정
if (project == rootProject) {
    tasks.register('testAll') {
        dependsOn subprojects.test
        doLast {
            println 'All tests completed'
        }
    }
}


